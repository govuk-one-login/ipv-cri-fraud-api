plugins {
	id 'java'
	id 'io.freefair.aspectj.post-compile-weaving' version '8.6'
}

defaultTasks 'clean', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

dependencies {

	var dependencyVersions = [
		// ---------------------------------------------------------
		// cri_common_lib dependencies should match the ipv-cri-lib version
		// Workaround until dependency resolution is fix.
		// ---------------------------------------------------------
		cri_common_lib_version             : "3.0.1",

		// CRI_LIB aws
		aws_sdk_version                    : "2.27.3",
		aws_lambda_events_version          : "3.11.0",
		aws_embedded_metrics_version       : "1.0.6",

		// CRI_LIB nimbus
		nimbusds_oauth_version             : "11.2",
		nimbusds_jwt_version               : "9.36",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "1.12.0",
		aws_powertools_metrics_version     : "1.18.0",
		aws_powertools_parameters_version  : "1.12.0",

		// ---------------------------------------------------------
		// AC Test Dependencies (Fraud CRI)
		// ---------------------------------------------------------

		aws_lambda_core_version            : "1.2.1",
		// Jackson Addons/ needs to track the aws sdk version of jackson
		jackson_version                    : "2.15.2",
		// Code weaving (lombok+powertools)
		aspectjrt_version                  : "1.9.21",

		// Test
		junit_version                      : "5.10.3",
		hamcrest_version                   : "3.0",
		mockito_version                    : "4.3.1",
		// testFixturesImplementation

		// acceptance tests Implementation
		// Update these together
		cucumber_version                   : "7.18.1",
		selenium_version                   : "4.22.0",
		axe_core_selenium_version          : "4.9.1",
		webdrivermanager_version           : "5.6.4",

		// acceptance tests testImplementation
		rest_assured_version               : "5.3.0",
		cucumber_junit_version             : "7.18.1"
	]

	implementation "uk.gov.account:cri-common-lib:${dependencyVersions.cri_common_lib_version}",
			"software.amazon.awssdk:kms:${dependencyVersions.aws_sdk_version}",
			"com.fasterxml.jackson.core:jackson-core",
			"com.fasterxml.jackson.core:jackson-databind",
			"com.fasterxml.jackson.core:jackson-annotations",
			"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
			"com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${dependencyVersions.jackson_version}",
			"com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
			"com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbusds_jwt_version}",
			"org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit_version}",
			"org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit_version}",
			"org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit_version}",
			"org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito_version}",
			"org.mockito:mockito-inline:${dependencyVersions.mockito_version}",
			"org.hamcrest:hamcrest:${dependencyVersions.hamcrest_version}",
			"org.json:json:20230618",
			"org.aspectj:aspectjrt:${dependencyVersions.aspectjrt_version}"

	implementation "io.cucumber:cucumber-java:${dependencyVersions.cucumber_version}",
			"org.seleniumhq.selenium:selenium-java:${dependencyVersions.selenium_version}",
			"io.github.bonigarcia:webdrivermanager:${dependencyVersions.webdrivermanager_version}",
			"com.deque.html.axe-core:selenium:${dependencyVersions.axe_core_selenium_version}"

	testImplementation "io.rest-assured:rest-assured:${dependencyVersions.rest_assured_version}",
			"io.cucumber:cucumber-junit:${dependencyVersions.cucumber_junit_version}"

	aspect "software.amazon.lambda:powertools-logging:${dependencyVersions.aws_powertools_logging_version}",
			"software.amazon.lambda:powertools-metrics:${dependencyVersions.aws_powertools_metrics_version}",
			"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_parameters_version}"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	useJUnitPlatform()
	systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
}

//*************************************//
//                                     //
//    Test tasks                       //
//                                     //
//*************************************//

task cucumber() {
	dependsOn assemble, testClasses

	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--tags',
				"${tags}",
				'--glue',
				'gov/di_ipv_fraud/step_definitions',
				'src/test/resources/features/' ,
				'--plugin',
				'html:build/test-results/cucumber.html',
				'--plugin',
				'json:build/test-results/cucumber.json'
			]
		}
	}
}

task smokeTestBuild() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--plugin',
				'html:target/cucumber-report/index.html',
				'--glue',
				'gov/di_ipv_fraud/step_definitions',
				'src/test/resources/features/',
				'--tags',
				'@build-fraud'
			]
		}
	}
}

task smokeTestStaging() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'pretty',
				'--glue',
				'gov/di_ipv_fraud/step_definitions',
				'src/test/resources/features/',
				'--tags',
				'@staging-fraud'
			]
		}
	}
}

apply plugin: 'java'
