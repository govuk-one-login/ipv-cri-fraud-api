plugins {
	id "java"
	id "org.sonarqube"
	id "com.diffplug.spotless"
	id "io.freefair.lombok" version "6.6.3"
}

defaultTasks 'clean', 'spotlessApply', 'build'

ext {
	dependencyVersions = [
		jackson_version             : "2.13.1",
		aws_sdk_version             : "2.21.9",
		aws_lambda_events_version   : "3.11.0",
		aws_powertools_version      : "1.12.2",
		nimbusds_oauth_version      : "9.25",
		nimbusds_jwt_version        : "9.15.1",
		protobuf_version            : "3.19.4",
		junit                       : "5.8.2",
		mockito                     : "4.3.1",
		cri_common_lib              : "1.4.5",
		lombok_version              : "1.18.22"
	]
}

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

subprojects {
	apply plugin: 'io.freefair.lombok'

	java {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	repositories {
		//		mavenCentral()
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
	}

	configurations {
		aws
		aws_apache_http_client
		dynamodb
		jackson
		lambda
		sqs
		kms
		lambda_tests
		lettuce
		nimbus
		ssm
		tests
		test_runtime
		powertools
		mockito
		logging_runtime
		cri_common_lib
		lombok
	}

	dependencies {
		cri_common_lib "uk.gov.account:cri-common-lib:${dependencyVersions.cri_common_lib}"

		aws platform("software.amazon.awssdk:bom:${dependencyVersions.aws_sdk_version}")

		aws_apache_http_client "software.amazon.awssdk:apache-client:${dependencyVersions.aws_sdk_version}"

		dynamodb "software.amazon.awssdk:dynamodb",
				"software.amazon.awssdk:dynamodb-enhanced"

		lambda "software.amazon.awssdk:lambda",
				"com.amazonaws:aws-lambda-java-events:${dependencyVersions.aws_lambda_events_version}",
				"software.amazon.lambda:powertools-parameters:${dependencyVersions.aws_powertools_version}"

		lambda_tests "software.amazon.awssdk:aws-lambda-java-tests:1.1.1"

		sqs "software.amazon.awssdk:sqs"

		kms "software.amazon.awssdk:kms"

		jackson "com.fasterxml.jackson.core:jackson-core:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.core:jackson-annotations:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${dependencyVersions.jackson_version}",
				"jakarta.xml.bind:jakarta.xml.bind-api:3.0.1",
				"com.sun.xml.bind:jaxb-impl:3.0.2"

		powertools "software.amazon.lambda:powertools-logging:${dependencyVersions.aws_powertools_version}",
				"software.amazon.lambda:powertools-metrics:${dependencyVersions.aws_powertools_version}"

		nimbus "com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbusds_oauth_version}",
				"com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbusds_jwt_version}"

		tests "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit}",
				"org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit}",
				"org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito}",
				"org.mockito:mockito-inline:${dependencyVersions.mockito}",
				"org.hamcrest:hamcrest:2.2"

		test_runtime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"
	}
	apply plugin: 'java'
}

sonarqube {
	properties {
		property "sonar.projectKey", "ipv-cri-fraud-api"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
