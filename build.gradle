plugins {
	id "java"
	id "idea"
	id "org.sonarqube" version "5.0.0.4638"
	id "io.freefair.lombok" version "8.4"
	id "jacoco"
	id "jacoco-report-aggregation"
	id "com.diffplug.spotless" version "6.25.0"
	id "org.owasp.dependencycheck" version "10.0.3"
}

defaultTasks 'clean', 'spotlessApply', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

ext {
	aws_powertools_version = "1.18.0"
	dependencyVersions = [
		// ---------------------------------------------------------
		// cri_common_lib dependencies should match the ipv-cri-lib version
		// Workaround until dependency resolution is fixed.
		// ---------------------------------------------------------

		cri_common_lib_version             : "3.0.5",

		// AWS SDK
		aws_sdk_version                    : "2.28.7",
		aws_lambda_events_version          : "3.11.6",

		// Nimbus Oauth
		nimbusds_oauth_version             : "11.20",
		nimbusds_jwt_version               : "9.41.1",

		// CRI_LIB powertools
		aws_powertools_logging_version     : "${aws_powertools_version}",
		aws_powertools_metrics_version     : "${aws_powertools_version}",
		aws_powertools_parameters_version  : "${aws_powertools_version}",

		// ---------------------------------------------------------
		// Fraud CRI Dependencies
		// ---------------------------------------------------------

		// AWS  aws-lambda-java-libs see https://github.com/aws/aws-lambda-java-libs
		aws_lambda_core_version            : "1.2.1",
		// Jackson Addons/ needs to track the aws sdk version of jackson
		jackson_version                    : "2.15.2",
		// Code weaving (lombok+powertools)
		aspectjrt_version                  : "1.9.21",

		// CRI Apache HTTP Client see https://hc.apache.org/httpcomponents-client-4.5.x/current/httpclient/dependencies.html
		httpcomponents_core_version        : "4.4.16",
		httpcomponents_client_version      : "4.5.14",

		// Test
		junit_version                      : "5.11.0",
		hamcrest_version                   : "3.0",
		mockito_version                    : "4.3.1",
		wiremock_version                   : "3.0.1",
		webcompere_version                 : "2.1.6",
		// testFixturesImplementation

		// Contract Tests
		pact_provider_version              : "4.6.14",
		slf4j_log4j12_version              : "2.0.13", // For contract test debug
	]

	// Sets the version used on the lambda + lib (ac tests have separate dependencies)
	javaCompatibility = [
		source : JavaVersion.VERSION_17,
		target : JavaVersion.VERSION_17
	]

	// Code Coverage (Lines/Branches) cannot be below this value on a per sub project basis
	minUnitTestLineCoverage = 0.80
	minUnitTestBranchCoverage = 0.80
}

sonar {
	properties {
		property "sonar.projectKey", "ipv-cri-fraud-api"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.coverage.jacoco.xmlReportPath", layout.buildDirectory.file("reports/jacoco/reports/reports.xml")
		property "sonar.dependencyCheck.htmlReportPath", layout.buildDirectory.file("reports/dependency-check-report.html")
		property "sonar.issue.ignore.multicriteria", "e1"
		property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S107"
		property "sonar.issue.ignore.multicriteria.e1.resourceKey", "**/*.java"
	}
}

// This generates an aggregate test report at "${buildDir}/reports/jacoco/reports/reports.xml"
reporting {
	reports {
		reports(JacocoCoverageReport) {
			testType = TestSuiteType.UNIT_TEST
		}
	}
}

dependencies {
	jacocoAggregation project(':lib'),
			project("lambdas:fraudcheck"),
			project("lambdas:issuecredential")
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

dependencyCheck {
	failBuildOnCVSS=7
	suppressionFiles= List.of("dependencyCheckAnalyzeSuppressionFiles/Netty-CVE-2023-4586.xml")
	autoUpdate=true
}

subprojects {
	apply plugin: 'org.sonarqube'
	apply plugin: 'io.freefair.lombok'
	apply plugin: 'org.owasp.dependencycheck'

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
		//flatDir {
		//	dirs '<Location of your projects absolute path>/di-ipv-cri-lib/build/libs'
		//}
	}

	plugins.withId('java') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	plugins.withId('java-library') {
		sourceCompatibility = "${javaCompatibility.source}"
		targetCompatibility = "${javaCompatibility.target}"
	}

	task allDeps(type: DependencyReportTask) {}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}
